buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'biz.aQute.bnd:biz.aQute.bnd.gradle:6.4.0'
    }
}

plugins {
    id 'com.github.spotbugs' version '5.0.14'
}

group 'com.fifesoft'
// NOTE: Local Java 8:  /Library/Java/JavaVirtualMachines/jdk1.8.0_191.jdk/Contents/Home

allprojects {

    repositories {
        mavenCentral()
        maven {
            url 'https://oss.sonatype.org/content/repositories/snapshots'
        }
    }

    wrapper {
        gradleVersion = '8.2.1'
    }
}

subprojects {

    apply plugin: 'java'
    apply plugin: 'checkstyle'
    apply plugin: 'com.github.spotbugs'

    test {
        useJUnitPlatform()
    }

    checkstyle {
        toolVersion = '9.3'
        configDirectory = file("$rootProject.projectDir/config/checkstyle")
    }

    spotbugs {
        // Relative to subprojects
        includeFilter = file('../config/spotbugs-exclude.xml')
    }

    spotbugsMain {
        reports {
            html {
                required = true
            }
            xml {
                required = false
            }
        }
    }
    spotbugsTest {
        reports {
            html {
                required = true
            }
            xml {
                required = false
            }
        }
    }

    dependencies {
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.3'
        testImplementation 'org.mockito:mockito-inline:4.10.0'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.3'
    }

    compileJava {
        sourceCompatibility javaVersion.toString()
        targetCompatibility javaVersion.toString()
        options.debug = true
        options.debugOptions.debugLevel = 'source,vars,lines'
        options.compilerArgs << '-Xlint:deprecation' << '-Xlint:unchecked'
    }
}

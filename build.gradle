buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'biz.aQute.bnd:biz.aQute.bnd.gradle:5.3.0'
    }
}

plugins {
    id 'com.github.spotbugs' version '4.7.1'
}

group 'com.fifesoft'
// NOTE: Local Java 8:  /Library/Java/JavaVirtualMachines/jdk1.8.0_191.jdk/Contents/Home

allprojects {

    repositories {
        mavenCentral()
        maven {
            url 'https://oss.sonatype.org/content/repositories/snapshots'
        }
    }

    wrapper {
        gradleVersion = '7.0.2'
    }
}

subprojects {

    apply plugin: 'java'
    apply plugin: 'checkstyle'
    apply plugin: 'com.github.spotbugs'

    checkstyle {
        toolVersion = '8.43'
        configDirectory = file("$rootProject.projectDir/config/checkstyle")
    }

    spotbugs {
        // Relative to subprojects
        includeFilter = file('../config/spotbugs-exclude.xml')
    }

    spotbugsMain {
        reports {
            html {
                enabled = true
            }
            xml {
                enabled = false
            }
        }
    }
    spotbugsTest {
        reports {
            html {
                enabled = true
            }
            xml {
                enabled = false
            }
        }
    }

    dependencies {
        testImplementation 'junit:junit:4.13.2'
    }

    compileJava {
        sourceCompatibility javaVersion.toString()
        targetCompatibility javaVersion.toString()
        options.debug = true
        options.debugOptions.debugLevel = 'source,vars,lines'
        options.compilerArgs << '-Xlint:deprecation' << '-Xlint:unchecked'
    }
}

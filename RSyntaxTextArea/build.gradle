import static org.gradle.api.JavaVersion.*

['base', 'jacoco', 'biz.aQute.bnd.builder', 'distribution', 'maven-publish', 'signing'].each { apply plugin: it }

// We require building with JDK 11 or later.  We turn off doclint since our
// generated *TokenMakers have horrible documentation (see https://github.com/jflex-de/jflex/issues/182)
assert current().isJava11Compatible()
allprojects {
	tasks.withType(Javadoc) {
		options.addStringOption('Xdoclint:none', '-quiet')
	}
}

base {
    archivesName = 'rsyntaxtextarea'
}
ext.isReleaseVersion = !project.version.endsWith('SNAPSHOT')

jacocoTestReport {
	reports {
		xml.required = true // codecov depends on xml format report
		html.required = true
	}
}

java {
    withSourcesJar()
    withJavadocJar()
}
jar {
    manifest {
        attributes('Specification-Title': 'RSyntaxTextArea',
                'Automatic-Module-Name': 'org.fife.RSyntaxTextArea', // jdk 9+ module name
                'Specification-Version': version,
                'Implementation-Title': 'org.fife.ui',
                'Implementation-Version': version,
                'Bundle-License': 'BSD-3-Clause',
                // Not sure why Require-Capability is not being added by the osgi plugin...
                'Require-Capability': 'osgi.ee;filter:="(&(osgi.ee=JavaSE)(version=' + javaVersion + '))"')
    }
}
test {
    testLogging {
        events 'failed' //, 'passed', 'skipped', 'standardOut', 'standardError'

        showExceptions true
        exceptionFormat 'full'
        showCauses true
        showStackTraces true

        showStandardStreams = false
    }
}

publishing {
    repositories {
        maven {
            def releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
            url = isReleaseVersion ? releasesRepoUrl : snapshotsRepoUrl
            credentials { // Credentials usually kept in user's .gradle/gradle.properties
                // We must defensively check for these properties so CI builds work
                username = project.hasProperty('ossrhToken') ? ossrhToken : 'unknown'
                password = project.hasProperty('ossrhTokenPassword') ? ossrhTokenPassword : 'unknown'
            }
        }
    }
    publications {
        maven(MavenPublication) {

            groupId = 'com.fifesoft'
            artifactId = 'rsyntaxtextarea'
            version = version

            from components.java

            pom {
                name = 'rsyntaxtextarea'
                description = 'RSyntaxTextArea is the syntax highlighting text editor for Swing applications. ' +
                        'Features include syntax highlighting for 40+ languages, code folding, code completion, ' +
                        'regex find and replace, macros, code templates, undo/redo, line numbering and bracket ' +
                        'matching.'
                url = 'https://github.com/bobbylight/rsyntaxtextarea'
                inceptionYear = '2003'
                packaging = 'jar'
                licenses {
                    license {
                        name = 'BSD-3-Clause'
                        url = 'https://github.com/bobbylight/RSyntaxTextArea/tree/master/RSyntaxTextArea/src/main/resources/META-INF/LICENSE'
                    }
                }
                developers {
                    developer {
                        name = 'Robert Futrell'
                    }
                }
                scm {
                    url = 'https://github.com/bobbylight/RSyntaxTextArea'
                    connection = 'scm:git:git://github.com/bobbylight/RSyntaxTextArea'
                    developerConnection = 'scm:git:git@github.com:bobbylight/RSyntaxTextArea'
                    if (isReleaseVersion) {
                        tag = project.version
                    }
                }
            }
        }
    }
}

signing {
    // Don't require signing for e.g. ./gradlew install
    required { gradle.taskGraph.hasTask('publish') && isReleaseVersion }
    sign publishing.publications.maven
}
tasks.withType(Sign) {
    onlyIf { isReleaseVersion }
}
